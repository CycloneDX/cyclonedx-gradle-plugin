plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '1.1.0'
    id 'maven-publish'
    id 'groovy'
    id 'org.cyclonedx.bom' version '1.7.2'
}

repositories {
    mavenCentral()
}

dependencies {
    implementation(group: 'org.cyclonedx', name: 'cyclonedx-core-java', version: '7.3.0') {
        // gradle-api already includes an slf4j binding
        exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
    }

    implementation 'commons-codec:commons-codec:1.15'
    implementation 'commons-io:commons-io:2.11.0'
    implementation 'org.apache.maven:maven-core:3.8.6'

    testImplementation gradleTestKit()
    testImplementation("org.spockframework:spock-core:2.2-M1-groovy-3.0") {
        exclude module: 'groovy-all'
    }
}

test {
    useJUnitPlatform()
}

project.ext.organization = 'CycloneDX'
group = 'org.cyclonedx'
version = '1.7.2'

// populate properties into plugin.properties at build time
ant.propertyfile(file: "src/main/resources/plugin.properties") {
  entry(key: "vendor", value: project.ext.organization)
  entry(key: "name", value: project.name)
  entry(key: "version", value: project.version)
}

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

pluginBundle {
    website = 'https://cyclonedx.org'
    vcsUrl = 'https://github.com/CycloneDX/cyclonedx-gradle-plugin.git'
    tags = [ 'cyclonedx', 'dependency', 'dependencies', 'owasp', 'inventory', 'bom', 'sbom' ]
}

gradlePlugin {
    plugins {
        cycloneDxPlugin {
            id = 'org.cyclonedx.bom'
            displayName = 'CycloneDX BOM Generator'
            description = 'The CycloneDX Gradle plugin creates an aggregate of all direct and transitive dependencies of a project and creates a valid CycloneDX Software Bill of Materials (SBOM).'
            implementationClass = 'org.cyclonedx.gradle.CycloneDxPlugin'
        }
    }
}
