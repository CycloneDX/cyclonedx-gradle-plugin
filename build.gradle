plugins {
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.12.0'
    id 'maven-publish'
    id 'groovy'
    id 'org.cyclonedx.bom' version '1.4.0' apply true
}

repositories {
    jcenter()
    mavenCentral()
}

dependencies {
    compile(group: 'org.cyclonedx', name: 'cyclonedx-core-java', version: '5.0.4') {
        // gradle-api already includes an slf4j binding
        exclude group: 'org.apache.logging.log4j', module: 'log4j-slf4j-impl'
    }
    compile 'commons-codec:commons-codec:1.15'
    compile 'org.apache.maven:maven-core:3.5.0'

    testImplementation gradleTestKit()
    testImplementation("org.spockframework:spock-core:1.1-groovy-2.4") {
        exclude module: 'groovy-all'
    }
}

group = 'org.cyclonedx'
version = '1.5.1'

sourceCompatibility = JavaVersion.VERSION_1_8
targetCompatibility = JavaVersion.VERSION_1_8

tasks.withType(JavaCompile) {
    options.encoding = 'UTF-8'
}

def pluginProperties = [
        'vendor': 'CycloneDX',
        'name': 'CycloneDX Gradle plugin',
        'version': project.version,
        'groupId': project.group,
        'artifactId': project.name,
] as Properties

def filterPluginProperties = tasks.register('filterPluginProperties', Copy) {
    destinationDir = file("$buildDir/resources/filter")
    inputs.properties pluginProperties
    from('src/main/filter')
    expand(pluginProperties)
}
sourceSets.main.resources.srcDirs filterPluginProperties

pluginBundle {
    website = 'https://cyclonedx.org'
    vcsUrl = 'https://github.com/CycloneDX/cyclonedx-gradle-plugin.git'
    tags = [ 'cyclonedx', 'dependency', 'dependencies', 'owasp', 'inventory', 'bom', 'sbom' ]
    plugins {
        cycloneDxPlugin {
            displayName = 'CycloneDX BOM Generator'
            description = 'The CycloneDX Gradle plugin creates an aggregate of all direct and transitive dependencies of a project and creates a valid CycloneDX Software Bill of Materials (SBOM).'
        }
    }
}

gradlePlugin {
    plugins {
        cycloneDxPlugin {
            id = 'org.cyclonedx.bom'
            implementationClass = 'org.cyclonedx.gradle.CycloneDxPlugin'
        }
    }
}
